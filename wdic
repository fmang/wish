#!/usr/bin/perl -CA

use strict;
use warnings;
use utf8;

use File::Path qw(make_path);
use Getopt::Long qw(:config no_auto_abbrev);
use Term::ANSIColor;
use Wish::Edict2;
use Wish::KanjiDic;

binmode(STDOUT, ":utf8");

my $dbdir = 'db';
my $load_mode;
my @kanjidic_files;
my @edict_files;

GetOptions(
	'database|db|d=s' => \$dbdir,
	'load|l' => \$load_mode,
	'kanjidic=s' => \@kanjidic_files,
	'edict=s' => \@edict_files,
);

if (!$load_mode and @kanjidic_files || @edict_files) {
	die("Cannot load a dictionary file without --load.\n");
}

if ($load_mode and @ARGV) {
	die("Please don't load and search at the same time.\n");
}

if ($load_mode && ! -d $dbdir) {
	print(STDERR "Creating the database directory $dbdir...\n");
	make_path($dbdir);
}

my $kanjidic = Wish::KanjiDic->new($dbdir, readonly => !$load_mode);
my $edict = Wish::Edict2->new($dbdir, readonly => !$load_mode);

$kanjidic && $edict or die("Couldn't open the dictionary database: $!.\n");

for my $file (@kanjidic_files) {
	print(STDERR "Loading KANJIDIC from $file...\n");
	my $s = $kanjidic->load($file);
	$s or warn("Couldn't load KANJIDIC from $file: $!.\n");
	$s and print(STDERR "Successfully loaded KANJIDIC from $file.\n");
}

for my $file (@edict_files) {
	print(STDERR "Loading EDICT2 from $file...\n");
	my $s = $edict->load($file);
	$s or warn("Couldn't load EDICT2 from $file: $!.\n");
	$s and print(STDERR "Successfully loaded EDICT2 from $file.\n");
}

sub print_kanji {
	my $k = shift;
	print(colored($k->{kanji}, 'blue'));
	my $prefix = ' ';
	my %elems = (
		'on' => '音',
		'kun' => '訓',
		'nanori' => '名',
		'english' => '英',
	);
	for my $f (qw/on kun nanori english/) {
		next unless defined $k->{$f};
		print($prefix, colored($elems{$f}, 'green'));
		$prefix = ' ';
		for (@{$k->{$f}}) {
			$f eq 'kun' and s/\.(.*)$/colored("·$1", 'white')/e;
			print($prefix, "$_\n");
			$prefix = '      ';
		}
		$prefix = '   ';
	}
}

for my $q (@ARGV) {
	print("\n");
	if ($q =~ /^[1-4]-[0-9]*-[0-9]*$/) {
		print(colored("ー SKIP $q\n\n", 'yellow'));
		my @r = $kanjidic->skip_lookup($q);
		print(@r ? join('', @r) : colored('Nothing found.', 'white'), "\n");
		next;
	}
	print(colored("ー $q\n", 'yellow'));
	for (sort(Wish::Edict2::kanjis($q))) {
		print("\n");
		print_kanji($kanjidic->lookup($_));
	}
	print("\n");
	print(colored("Nothing found.\n", 'white'));
}

print("\n");
